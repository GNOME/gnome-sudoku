desktop_conf = configuration_data()
desktop_conf.set('icon', application_id)
desktop_file = i18n.merge_file(
  input: configure_file(
    input: 'org.gnome.Sudoku.desktop.in.in',
    output: '@BASENAME@',
    configuration: desktop_conf
  ),
  output: '@0@.desktop'.format(application_id),
  install: true,
  install_dir: join_paths(datadir, 'applications'),
  po_dir: '../po',
  type: 'desktop'
)
desktop_file_validate = find_program('desktop-file-validate', required: false)
if desktop_file_validate.found()
  test(
    'validate-desktop',
    desktop_file_validate,
    args: [
      desktop_file.full_path()
    ],
    depends: [
      desktop_file
    ],
  )
endif

metainfo_conf = configuration_data()
metainfo_conf.set('app-id', application_id)
metainfo_file = i18n.merge_file(
  input: configure_file(
    input: 'org.gnome.Sudoku.metainfo.xml.in.in',
    output: '@BASENAME@',
    configuration: metainfo_conf
  ),
  output: '@0@.metainfo.xml'.format(application_id),
  install: true,
  install_dir: join_paths(datadir, 'metainfo'),
  po_dir: '../po'
)
appstreamcli = find_program('appstreamcli', required: false)
if appstreamcli.found()
  test(
    'validate-metainfo', appstreamcli,
    args: [
      'validate', '--no-net', metainfo_file.full_path()
    ],
    depends: [
      metainfo_file
    ],
  )
endif

install_data('org.gnome.Sudoku.gschema.xml',
  install_dir: join_paths(datadir, 'glib-2.0', 'schemas')
)

resource_conf = configuration_data()
resource_conf.set('app-id', application_id)
resource_files = configure_file(
  input: 'gnome-sudoku.gresource.xml.in',
  output: '@BASENAME@',
  configuration: resource_conf
)
resources = gnome.compile_resources('gnome-sudoku',
  resource_files,
  dependencies: metainfo_file,
  source_dir: meson.current_source_dir()
)

install_man('@0@.6'.format(meson.project_name()))

icondir = join_paths(datadir, 'icons', 'hicolor')

install_data('icons/hicolor/scalable/@0@.svg'.format(application_id),
  install_dir: join_paths(icondir, 'scalable', 'apps')
)

install_data('icons/hicolor/symbolic/org.gnome.Sudoku-symbolic.svg',
  install_dir: join_paths(icondir, 'symbolic', 'apps'),
  rename: '@0@-symbolic.svg'.format(application_id)
)

# Make D-Bus activatable
service_conf = configuration_data()
service_conf.set('bindir', join_paths(prefix, bindir))
service_conf.set('app-id', application_id)

service = 'org.gnome.Sudoku.service'

configure_file (
  input: service + '.in',
  output: '@0@.service'.format(application_id),
  install: true,
  install_dir: join_paths(prefix, datadir, 'dbus-1', 'services'),
  configuration: service_conf
)
